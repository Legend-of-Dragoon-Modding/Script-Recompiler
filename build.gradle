plugins {
  id 'java'
  id 'maven-publish'
  id 'signing'
}

group 'org.legendofdragoon'
version '0.5.9'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

java {
  sourceCompatibility = targetCompatibility = JavaVersion.VERSION_25
}

compileJava.options.encoding = 'UTF-8'

repositories {
  mavenCentral()
}

dependencies {
  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.24.3'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.24.3'
  implementation group: 'org.fusesource.jansi', name: 'jansi', version: '2.4.1'
  implementation group: 'com.opencsv', name: 'opencsv', version: '5.9'
  implementation group: 'commons-cli', name: 'commons-cli', version: '1.6.0'
  implementation group: 'io.github.java-diff-utils', name: 'java-diff-utils', version: '4.12'
}

tasks.register('sourceJar', Jar) {
  from sourceSets.main.allJava
}

tasks.withType(Sign).configureEach {
  onlyIf { isReleaseVersion }
}

java {
  withJavadocJar()
  withSourcesJar()
}

artifacts {
  archives javadocJar, sourcesJar
}

publishing {
  repositories {
    maven {
      final def releaseRepo = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
      final def snapshotRepo = "https://ossrh-staging-api.central.sonatype.com/content/repositories/snapshots/"
      url = isReleaseVersion ? releaseRepo : snapshotRepo
      credentials {
        username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
        password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      artifactId = 'script-recompiler'
      from components.java
      pom {
        name = 'Severed Chains Script Decompiler and Recompiler'
        description = 'Tools for working with Legend of Dragoon scripts'
        url = 'https://github.com/Legend-of-Dragoon-Modding/Script-Recompiler'
        packaging = 'jar'
        licenses {
          license {
            name = 'GNU Affero General Public License, Version 3.0'
            url = 'https://www.gnu.org/licenses/agpl-3.0.en.html'
          }
        }
        developers {
          developer {
            id = 'Monoxide'
            name = 'Corey Frenette'
            email = 'lordmonoxide@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/Legend-of-Dragoon-Modding/Script-Recompiler.git'
          developerConnection = 'scm:git:ssh://github.com/Legend-of-Dragoon-Modding/Script-Recompiler.git'
          url = 'https://github.com/Legend-of-Dragoon-Modding/Script-Recompiler/'
        }
      }
    }
  }
}

tasks.register('promote') {
  // extra manual step required by the Maven Central Publishing Portal which replaced the OSSRH service in June 2025
  // https://central.sonatype.org/news/20250326_ossrh_sunset/
  doLast {
    final def url = "https://ossrh-staging-api.central.sonatype.com/manual/upload/defaultRepository/org.legendofdragoon"
    final def token = "${ossrhUsername}:${ossrhPassword}".bytes.encodeBase64().toString()
    final def conn = new URL(url).openConnection()
    conn.setRequestMethod("POST")
    conn.setRequestProperty("Authorization", "Bearer ${token}")
    final def status = conn.responseCode
    if(status == 200) {
      project.logger.lifecycle("Successfully promoted repository, ready for review at https://central.sonatype.com/publishing/deployments");
    } else {
      final def errorStream = conn.errorStream
      final def errorBody = errorStream ? errorStream.text : "No error body available"
      project.logger.error("Failed to promote repository")
      project.logger.error("Response code: ${status}")
      project.logger.error("Response message: ${conn.responseMessage}")
      project.logger.error("Error body: ${errorBody}")
    }
  }
}

tasks.matching { it.name == 'publish' }.configureEach { final publishTask ->
  publishTask.finalizedBy tasks.promote
}

signing {
  sign publishing.publications.mavenJava
}

javadoc {
  if(JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
}
